@tool
class_name HenApi extends Node

const HENGO_CACHE_PATH = 'res://.godot/hengo/'
const EXTENSION_API_PATH = 'res://.godot/hengo/extension_api.json'
const EXTENSION_API_COMPRESSED_PATH = 'res://.godot/hengo/api_compressed.bin'
const API_VERSION: int = 2

var api_data: Dictionary = {}
var timer: SceneTreeTimer


enum Type {
	ENUM,
	METHOD,
	UTILITIES
}


class CompressedData:
	var bytes: PackedByteArray
	var original_size: int

	func _init(_bytes: PackedByteArray, _original_size: int) -> void:
		bytes = _bytes
		original_size = _original_size


func _ready() -> void:
	_generate_compressed_data()


func on_search_change(_text: String, _io_type: StringName = '', _type: StringName = '') -> void:
	debounce_search(0.3, search_api.bind(_text, _io_type, _type))


func check_hengo_folder() -> void:
	if not DirAccess.dir_exists_absolute(HENGO_CACHE_PATH):
		DirAccess.make_dir_absolute(HENGO_CACHE_PATH)


func _generate_compressed_data() -> void:
	check_hengo_folder()

	var compressed_api = await _get_compressed_api()
	if compressed_api:
		api_data = decompress_and_get_data(compressed_api)


func _get_compressed_api() -> CompressedData:
	if FileAccess.file_exists(EXTENSION_API_COMPRESSED_PATH):
		var data = load_compressed_data(EXTENSION_API_COMPRESSED_PATH)
		if data:
			return data

	return await _map_api()


func get_decompressed_data() -> Dictionary:
	if api_data.is_empty():
		print('Erro open compressed api')
		return {}

	return api_data


func search_api(_search_text: String, _io_type: StringName = '', _type: StringName = '') -> void:
	# offload to thread to verify performance and avoid blocking
	WorkerThreadPool.add_task(_threaded_search_api.bind(_search_text, _io_type, _type))


func _threaded_search_api(_search_text: String, _io_type: StringName, _type: StringName) -> void:
	var start: int = Time.get_ticks_usec()
	if api_data.is_empty():
		print('Erro open compressed api')
		return

	var data: Dictionary = api_data

	if not data:
		print('Not data')
		return

	var text: String = _search_text.strip_edges().to_lower()
	var results: Array = []
	var native_props: Dictionary = data.get(&'native_props')
	var mutex: Mutex = Mutex.new()

	print("Query: '%s'" % text)
	print("Query length: %d" % text.length())

	var thread_data: Dictionary = {
		"results": results,
		"text": text,
		"io_type": _io_type,
		"type": _type,
		"native_props": native_props,
		"data": data,
		"mutex": mutex
	}

	# task groups: classes, native classes, utilities, processors, map dependencies

	results.sort_custom(func(a, b): return a.score > b.score)

	call_deferred("_emit_search_results", results)

	var end: int = Time.get_ticks_usec()
	prints((end - start) / 1000., 'ms')


func _emit_search_results(results: Array) -> void:
	var signal_bus: HenSignalBus = Engine.get_singleton(&'SignalBus')
	signal_bus.request_code_search_type_result.emit(results)


func search_enum_data(_class_name: StringName, _class_data: Dictionary, _results: Array, _search_text: String, _io_type: StringName = '', _type: StringName = '', _native_props: Dictionary = {}) -> void:
	if _class_data.has(&"enums"):
		for enum_name: StringName in (_class_data.enums as Dictionary).keys():
			var enum_lower = enum_name.to_lower()
			var score = HenSearch.score_only(_search_text, enum_lower)
			var enum_data: Dictionary = _class_data.enums[enum_name] as Dictionary

			if score > 0:
				enum_data._class_name = _class_name
				enum_data.name = enum_name
				enum_data.score = score
				_results.append(enum_data)


func check_type_validity(_data: Dictionary, _io_type: StringName = '', _type: StringName = '', _native_props: Dictionary = {}) -> bool:
	var has_type: bool = false

	if _io_type == &'in':
		if HenUtils.is_type_relation_valid(
			_data.get(&'return_type', &'null'),
			_type
		):
			var params: Array = (_data.data as Dictionary).get(&'outputs', [])
			var idx: int = HenAPIProcessors.check_param_validity(params, _type, false)

			if idx != -1:
				_data.output_io_idx = idx
				has_type = true
		elif not _native_props.is_empty():
			var type: StringName = _data.get(&'return_type', &'')

			if _native_props.has(type):
				for prop: Dictionary in _native_props[type]:
					if HenUtils.is_type_relation_valid(prop.type, _type):
						has_type = true
						_data.use_props_only = true
						break
	elif _io_type == &'out':
		var params: Array = (_data.data as Dictionary).get(&'inputs', [])
		var idx: int = HenAPIProcessors.check_param_validity(params, _type, true)

		if idx != -1:
			_data.input_io_idx = idx
			has_type = true
		elif not _native_props.is_empty():
			for param: Dictionary in params:
				var type: StringName = param.get(&'type', &'')
				if _native_props.has(type):
					for prop: Dictionary in _native_props[type]:
						if HenUtils.is_type_relation_valid(_type, prop.type):
							has_type = true
							_data.use_props_only = true
							break
				if has_type: break

	else:
		has_type = true

	return has_type


func get_doc_for_ref(_type: StringName, _name: String) -> String:
	var data: Dictionary = get_decompressed_data()
	if data.is_empty():
		return ""

	if data.has(&"classes") and (data.classes as Dictionary).has(_type):
		var class_data: Dictionary = data.classes[_type]
		if class_data.has(&"methods") and (class_data.methods as Dictionary).has(_name):
			return (class_data.methods[_name] as Dictionary).get(&"description", "")

	if data.has(&"native_classes") and (data.native_classes as Dictionary).has(_type):
		var class_data: Dictionary = data.native_classes[_type]
		if class_data.has(&"methods") and (class_data.methods as Dictionary).has(_name):
			return (class_data.methods[_name] as Dictionary).get(&"description", "")

	if data.has(&"utilities") and (data.utilities as Dictionary).has(_name):
		return (data.utilities[_name] as Dictionary).get(&"description", "")

	return ""


func debounce_search(delay: float, callback: Callable) -> void:
	if timer:
		timer.timeout.disconnect(callback)
		timer = null

	timer = get_tree().create_timer(delay)
	timer.timeout.connect(callback)


# map just the necessary
func _map_api() -> CompressedData:
	var extension_api: FileAccess = await get_api_file()

	if not extension_api:
		print('Error dumping API')
		return

	var data: Dictionary = JSON.parse_string(extension_api.get_as_text())
	var new_api_data: Dictionary = {
		classes = map_classes(data),
		utilities = map_utilities(data),
		global_enums = map_global_enums(data),
		singletons = map_singletons(data),
		native_classes = map_native_classes(data),
		native_props = map_native_props(data)
	}

	var compressed_data: CompressedData = save_and_get_compressed_data(new_api_data.duplicate(true), EXTENSION_API_COMPRESSED_PATH)

	if FileAccess.file_exists(EXTENSION_API_PATH):
		DirAccess.remove_absolute(EXTENSION_API_PATH)

	return compressed_data


func get_api_file() -> FileAccess:
	print('Generating Godot Native Api...')
	print('Dumping Godot Extension Api...')
	if FileAccess.file_exists(EXTENSION_API_PATH):
		print('Found extension_api.json')
		return FileAccess.open(EXTENSION_API_PATH, FileAccess.READ)

	var pid = OS.create_process(OS.get_executable_path(), ['-q', '--headless', '--dump-extension-api-with-docs', '--path', '.godot/hengo/'])

	if pid > 0:
		while OS.is_process_running(pid):
			await Engine.get_main_loop().process_frame

	if FileAccess.file_exists(EXTENSION_API_PATH):
		print('Found extension_api.json')
		return FileAccess.open(EXTENSION_API_PATH, FileAccess.READ)

	print('Not Found extension_api.json.')
	return null


func map_native_props(_data: Dictionary) -> Dictionary:
	var dict: Dictionary = {}

	if _data.has(&'builtin_class_member_offsets'):
		for conf: Dictionary in _data.get(&'builtin_class_member_offsets'):
			if conf.has(&'classes'):
				for cls: Dictionary in conf.get(&'classes'):
					if not dict.has(cls.name):
						var members: Array = []

						for member: Dictionary in cls.get(&'members'):
							members.append({
								name = member.member,
								type = member.meta
							})

						dict[cls.name] = members

	return dict


func map_native_classes(_data: Dictionary) -> Dictionary:
	var dict: Dictionary = {}

	if _data.has(&'builtin_classes'):
		for class_data: Dictionary in _data.get(&'builtin_classes'):
			var dt: Dictionary = {}

			if class_data.has(&'members'):
				dt.members = class_data.members

			if class_data.has(&'constants'):
				dt.constants = class_data.constants

			if class_data.has(&'description'):
				dt.description = class_data.description

			if _data.has(&'enums'):
				var enums: Dictionary = {}
				for enum_data: Dictionary in _data.get(&'enums'):
					var value_data: Dictionary = {}

					for enum_value: Dictionary in enum_data.get(&'values'):
						value_data.set(enum_value.name, {
							description = enum_value.description
						})

					enums.set(enum_data.name, value_data)

				dt.enums = enums

			# map methods
			if class_data.has(&'methods'):
				dt.methods = map_methods(class_data.get(&'methods'))

			dict.set(class_data.name, dt)

	return dict


func map_methods(_list: Array, _prop_data: Dictionary = {}) -> Dictionary:
	var dt: Dictionary = {}

	for method_data: Dictionary in _list:
		var method_dt: Dictionary = {
			params = []
		}

		if method_data.has(&'description'):
			method_dt.description = method_data.description
		elif not _prop_data.is_empty():
			var dsc: String = ''

			if _prop_data.has(method_data.name):
				var prop: Dictionary = _prop_data.get(method_data.name)

				method_dt.prop_name = prop.get(&'prop_name')

				if prop.has(&'is_getter'):
					method_dt.is_getter = true
				elif prop.has(&'is_setter'):
					method_dt.is_setter = true

			if method_data.has(&'description'):
				dsc = method_data.get(&'description')
			else:
				dsc = _prop_data.get(method_data.name).description \
				if _prop_data.has(method_data.name) and (_prop_data.get(method_data.name) as Dictionary).has(&'description') else ''

			method_dt.description = dsc

		if method_data.has(&'is_static') and method_data.get(&'is_static', false):
			method_dt.is_static = true

		if method_data.has(&'is_virtual') and method_data.get(&'is_virtual', false):
			method_dt.is_virtual = true

		if method_data.has(&'arguments'):
			for argument_data: Dictionary in method_data.get(&'arguments'):
				(method_dt.params as Array).append(argument_data)

		if method_data.has(&'return_type'):
			method_dt.return_type = method_data.return_type
		elif method_data.has(&'return_value'):
			if (method_data.return_value as Dictionary).has(&'type'):
				method_dt.return_type = method_data.return_value.type

		dt.set(method_data.name, method_dt)

	return dt


func map_singletons(_data: Dictionary) -> Array:
	var arr: Array = []
	if _data.has(&'singletons'):
		for singleton_data: Dictionary in _data.get(&'singletons'):
			arr.append(singleton_data.name)

	return arr


func map_global_enums(_data: Dictionary) -> Dictionary:
	var dict: Dictionary = {}

	if _data.has(&'global_enums'):
		for enum_data: Dictionary in _data.get(&'global_enums'):
			var value_data: Dictionary = {}

			for enum_value: Dictionary in enum_data.get(&'values'):
				value_data.set(enum_data.name, {
					name = enum_value.name,
					description = enum_value.description
				})

			dict.set(enum_data.name, value_data)

	return dict


func map_utilities(_data: Dictionary) -> Dictionary:
	return map_methods(_data.get(&'utility_functions', []))


func map_classes(_data: Dictionary) -> Dictionary:
	var dict: Dictionary = {}

	# classes: just map docs
	for class_data: Dictionary in _data.get(&'classes', []):
		var dt: Dictionary = {
			description = class_data.description if class_data.has('description') else '',
		}
		var prop_data: Dictionary = {}

		# map enums
		if class_data.has(&'enums'):
			var enums: Dictionary = {}
			for enum_data: Dictionary in class_data.get(&'enums'):
				for enum_value: Dictionary in enum_data.get(&'values'):
					enums.set(enum_data.name, {
						name = enum_value.name,
						description = enum_value.description if enum_value.has(&'description') else ""
					})

			dt.enums = enums

		# map props just to get description
		if class_data.has(&'properties'):
			for prop_dict: Dictionary in class_data.get(&'properties'):
				if prop_dict.has(&'setter'):
					prop_data.set(prop_dict.setter, {
						prop_name = prop_dict.name,
						is_setter = true,
						description = prop_dict.description if prop_dict.has(&'description') else "",
					})

				if prop_dict.has(&'getter'):
					prop_data.set(prop_dict.getter, {
						prop_name = prop_dict.name,
						is_getter = true,
						description = prop_dict.description if prop_dict.has(&'description') else "",
					})

		# map methods
		if class_data.has(&'methods'):
			dt.methods = map_methods(class_data.get(&'methods'), prop_data)

		dict.set(class_data.name, dt)

	return dict


func save_and_get_compressed_data(data: Variant, path_out: String) -> CompressedData:
	var raw_bytes = var_to_bytes(data)
	var compressed = raw_bytes.compress(FileAccess.COMPRESSION_ZSTD)
	var size: int = raw_bytes.size()
	var f = FileAccess.open(path_out, FileAccess.WRITE)

	if f == null:
		push_error('Error opening: ' + path_out)
		return null

	f.store_8(FileAccess.COMPRESSION_ZSTD)
	f.store_32(API_VERSION)
	f.store_pascal_string(Engine.get_version_info().string)
	f.store_32(size)
	f.store_buffer(compressed)
	f.close()

	return CompressedData.new(compressed, size)


func load_compressed_data(path_in: String) -> CompressedData:
	var f = FileAccess.open(path_in, FileAccess.READ)
	if f == null:
		push_error('Could not open: ' + path_in)
		return null

	var file_size = f.get_length()
	f.get_8()
	var version = f.get_32()
	var godot_version = f.get_pascal_string()

	if version != API_VERSION or godot_version != Engine.get_version_info().string:
		return null

	var original_size = f.get_32()
	# 4 bytes (size) + 4 bytes (version) + 1 byte (compression) + pascal string (4 bytes len + utf8 len)
	var header_size: int = 13 + godot_version.to_utf8_buffer().size()
	var compressed_bytes = f.get_buffer(file_size - header_size)
	f.close()

	return CompressedData.new(compressed_bytes, original_size)


func decompress_and_get_data(compressed_data: CompressedData) -> Variant:
	var decompressed: PackedByteArray = compressed_data.bytes.decompress(compressed_data.original_size, FileAccess.COMPRESSION_ZSTD)
	return bytes_to_var(decompressed)


func get_side_bar_list(_io_type: StringName = '', _type: StringName = '') -> Dictionary:
	return {}

